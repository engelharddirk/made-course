pipeline DBStopsPipeline{

    block DataExtractor oftype HttpExtractor { url: "https://download-data.deutschebahn.com/static/datasets/haltestellen/D_Bahnhof_2020_alle.CSV"; }
    block DBTextFileInterpreter oftype TextFileInterpreter {}
    block DBStopsCSVInterpreter oftype CSVInterpreter { delimiter: ";"; }
    block DBStopsSQLite oftype SQLiteLoader {
        table: "trainstops";
        file: "./trainstops.sqlite";
        dropTable: true;
    }

    block DBStopsTableInterpreter oftype TableInterpreter {
        header: true;
        columns:[
            "EVA_NR" oftype integer,
            "DS100" oftype text,
            "IFOPT" oftype IFOPT,
            "NAME" oftype text,
            "Verkehr" oftype VerkehrCode,
            "Laenge" oftype CoordinateDimension,
            "Breite" oftype CoordinateDimension,
            "Betreiber_Name" oftype text,
            "Betreiber_Nr" oftype integer,
        ];
    }

    block DBStopsColumnDeleter oftype ColumnDeleter {
        delete: [column J];
    }

    DataExtractor
        ->DBTextFileInterpreter
        ->DBStopsCSVInterpreter
        ->DBStopsColumnDeleter
        ->DBStopsTableInterpreter
        ->DBStopsSQLite;

    valuetype CoordinateDimension oftype decimal {
        constraints: [CoordinateDimensionConstraint];
    }

    constraint CoordinateDimensionConstraint oftype RangeConstraint {
        upperBound: 90;
        lowerBound: -90;
    }


    valuetype VerkehrCode oftype text {
        constraints: [VerkehrConstraint];
    }

    constraint VerkehrConstraint on text:
        value in [
            "FV", "RV", "nur DPN",
        ];

    valuetype IFOPT oftype text {
        constraints: [IFOPTConstraint];
    }

    constraint IFOPTConstraint oftype RegexConstraint {
        regex: /^[a-z]{2}:[0-9]+:[0-9]+(?::[0-9]+)?$/;
    }
}